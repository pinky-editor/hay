<exprs>    = expr
           | expr <whitespace>+ exprs
<literals> = literal
           | literal <whitespace>+ literals

whitespace = #'[ \t\r\n,]'

<expr>     = symbol | qsymbol | literal
<literal>  = nil | boolean | number | string | regex
           | quoted | keyword | qkeyword
           | vector | list | set | map | block

nil        = <'nil'>
boolean    = 'true' | 'false'
string     = <'"'> #'([^"\\]|\\.)*' <'"'>
regex      = <'#'> string

symbol     = symhead symrest*
qsymbol    = symbol <'/'> symbol
quoted     = <'\''> symbol
keyword    = <':'> symrest+
qkeyword   = <(':' ':')> symrest+
           | <':'> symbol <'/'> symrest+

<symhead>  = #'[a-zA-Z._+*!?<=>]' | '-' ! #'[0-9]'
<symrest>  = #'[a-zA-Z0-9._+*!?<=>#\'-]'

number     = sign? (integer | float | rational)
integer    = ('0' | #'[1-9]' #'[0-9]'*)
float      = ('0' | #'[1-9]' #'[0-9]'*) '.' #'[0-9]'+
rational   = integer <'/'> integer
sign       = #'[+-]' & #'[0-9]'

vector     =  <'['> <whitespace*> literals <whitespace*> <']'>
list       =  <'('> <whitespace*> literals <whitespace*> <')'>
set        = <'#{'> <whitespace*> literals <whitespace*> <'}'>
map        =  <'{'> <whitespace*> literals <whitespace*> <'}'>
block      = <'\'['> <whitespace*> exprs <whitespace*> <']'>
